unit UViewPedidos;

interface

uses
  Winapi.Windows, Winapi.Messages, System.SysUtils, System.Variants, System.Classes, Vcl.Graphics,
  Vcl.Controls, Vcl.Forms, Vcl.Dialogs, Vcl.Grids, Vcl.DBGrids, Vcl.StdCtrls,
  Vcl.Buttons, Vcl.ExtCtrls, Data.DB, Vcl.Mask, System.UITypes,
  Interfaces.IPedido, FireDAC.Stan.Intf,
  FireDAC.Stan.Option, FireDAC.Stan.Param, FireDAC.Stan.Error, FireDAC.DatS,
  FireDAC.Phys.Intf, FireDAC.DApt.Intf, FireDAC.Stan.StorageBin,
  FireDAC.Comp.DataSet, FireDAC.Comp.Client, Vcl.Menus,
  Controller.PedidoController,
  Controller.ItemPedidoController,
  WKConst;

type
  TFViewPedidos = class(TForm)
    PHead: TPanel;
    PRodape: TPanel;
    PViewPedidos: TPanel;
    BBIncluir: TBitBtn;
    BBAlterar: TBitBtn;
    BBExcluir: TBitBtn;
    BBSair: TBitBtn;
    LNR: TLabel;
    ENR: TEdit;
    DBGView: TDBGrid;
    DSViewPedidos: TDataSource;
    PFiltrar: TPanel;
    LEFiltroNumeroPedido: TLabeledEdit;
    LEFiltroNomeCliente: TLabeledEdit;
    BBAtualizar: TBitBtn;
    PViewItensPedido: TPanel;
    DBGViewItens: TDBGrid;
    PedidosMemTable: TFDMemTable;
    PMOptions: TPopupMenu;
    PMAtualizarItensdoPedido: TMenuItem;
    N1: TMenuItem;
    PMValorTotaldoPedido: TMenuItem;
    DSItensPedido: TDataSource;
    ItensMemTable: TFDMemTable;
    PedidosMemTableNumeroPedidos: TIntegerField;
    PedidosMemTableDataEmissaoPedidos: TDateTimeField;
    PedidosMemTableClientePedidos: TIntegerField;
    PedidosMemTableNomeClientes: TStringField;
    PedidosMemTableValorTotalPedidos: TFMTBCDField;
    ItensMemTableidItensPedido: TIntegerField;
    ItensMemTablePedidoItensPedido: TIntegerField;
    ItensMemTableProdutoItensPedido: TIntegerField;
    ItensMemTableQuantidadeItensPedido: TBCDField;
    ItensMemTableVlrUnitarioItensPedido: TBCDField;
    ItensMemTableVlrTotalItensPedido: TBCDField;
    N2: TMenuItem;
    PMProdutoMaisVendido: TMenuItem;
    BBProdutoMaisVendido: TBitBtn;
    ItensMemTableDescricaoProdutos: TStringField;
    procedure FormClose(Sender: TObject; var Action: TCloseAction);
    procedure BBSairClick(Sender: TObject);
    procedure BBIncluirClick(Sender: TObject);
    procedure DBGViewKeyDown(Sender: TObject; var Key: Word;
      Shift: TShiftState);
    procedure DBGViewDblClick(Sender: TObject);
    procedure BBExcluirClick(Sender: TObject);
    procedure BBAtualizarClick(Sender: TObject);
    procedure BBAlterarClick(Sender: TObject);
    procedure PMValorTotaldoPedidoClick(Sender: TObject);
    procedure FormShow(Sender: TObject);
    procedure DSViewPedidosDataChange(Sender: TObject; Field: TField);
    procedure PMAtualizarItensdoPedidoClick(Sender: TObject);
    procedure PMProdutoMaisVendidoClick(Sender: TObject);
    procedure BBProdutoMaisVendidoClick(Sender: TObject);
  private
    { Private declarations }
    FPedidoController: TPedidoController;
    FItemPedidoController: TItemPedidoController;

    procedure TratarDelete;
    procedure pCRUD(pAcao: TAcao);
    procedure pAtualizacao;
    procedure CallProdutoMaisVendido;
    procedure pValorTotaldoPedido;
  public
    { Public declarations }
    constructor Create(AOwner: TComponent); override;
    destructor Destroy; override;

  end;

var
  FViewPedidos: TFViewPedidos;

implementation

{$R *.dfm}

uses UDadosPedidos, UViewMaisVendido;

constructor TFViewPedidos.Create(AOwner: TComponent);
begin
  inherited;
  FPedidoController := TPedidoController.Create;
  FItemPedidoController := TItemPedidoController.Create;
end;

destructor TFViewPedidos.Destroy;
begin
  FPedidoController.Free;
  FItemPedidoController.Free;
  inherited;
end;

procedure TFViewPedidos.FormShow(Sender: TObject);
begin
  pAtualizacao;
end;

procedure TFViewPedidos.BBIncluirClick(Sender: TObject);
begin
  pCRUD(acIncluir);
end;

procedure TFViewPedidos.BBAlterarClick(Sender: TObject);
begin
  pCRUD(acAlterar);
end;

procedure TFViewPedidos.DBGViewDblClick(Sender: TObject);
begin
  BBAlterar.Click;
end;

procedure TFViewPedidos.BBExcluirClick(Sender: TObject);
begin
  TratarDelete;
end;

procedure TFViewPedidos.BBAtualizarClick(Sender: TObject);
begin
  pAtualizacao;
end;

procedure TFViewPedidos.DBGViewKeyDown(Sender: TObject; var Key: Word;
  Shift: TShiftState);
begin
  // Verifica se a tecla DELETE foi pressionada
  if Key = VK_DELETE then
  begin
    TratarDelete;
    Key := 0; // Evita que o DBGrid processe o DELETE automaticamente
  end;
end;

procedure TFViewPedidos.DSViewPedidosDataChange(Sender: TObject; Field: TField);
begin
  FItemPedidoController.CarregarDadosItensPedido(ItensMemTable,
    DSViewPedidos.DataSet.FieldByName('NumeroPedidos').AsString);
end;

procedure TFViewPedidos.PMAtualizarItensdoPedidoClick(Sender: TObject);
begin
  pAtualizacao;
end;

procedure TFViewPedidos.PMValorTotaldoPedidoClick(Sender: TObject);
begin
  pValorTotaldoPedido;
end;

procedure TFViewPedidos.PMProdutoMaisVendidoClick(Sender: TObject);
begin
  CallProdutoMaisVendido;
end;

procedure TFViewPedidos.BBProdutoMaisVendidoClick(Sender: TObject);
begin
  CallProdutoMaisVendido;
end;

procedure TFViewPedidos.pCRUD(pAcao: TAcao);
var
  FormPedido: TFDadosPedidos;
begin
  if (DSViewPedidos.DataSet.FieldByName('NumeroPedidos').IsNull) and
     (pAcao <> acIncluir) then
  begin
    Beep;
    ShowMessage('Selecione um registro válido '+cEOL+'para efetuar operação desejada!');
    Exit;
  end;

  if (pAcao = acExcluir) then
  begin
    if FPedidoController.ExcluirPedido(
       DSViewPedidos.DataSet.FieldByName('NumeroPedidos').AsInteger) then
      ShowMessage('Pedido excluído com sucesso!')
    else
      ShowMessage('Erro ao excluir pedido.');
  end
  else
  begin
    FormPedido := TFDadosPedidos.Create(Application);
    if (pAcao = acIncluir) then
    begin
      FormPedido.Caption := FormPedido.Caption + '-' + cAcaoIncluir;
      FormPedido.LENumeroPedido.Clear;
      FormPedido.DTPDataEmissao.DateTime := Now;
    end
    else
    begin
      FormPedido.Caption := FormPedido.Caption + '-' + cAcaoAlterar;
      FormPedido.LENumeroPedido.Text := DSViewPedidos.DataSet.FieldByName('NumeroPedidos').AsString;
      FormPedido.DTPDataEmissao.DateTime := DSViewPedidos.DataSet.FieldByName('DataEmissaoPedidos').AsDateTime;
      FormPedido.LECodigoCliente.Text := DSViewPedidos.DataSet.FieldByName('ClientePedidos').AsString;
      FormPedido.EDescCliente.Text := DSViewPedidos.DataSet.FieldByName('NomeClientes').AsString;
      FormPedido.LETotalPedido.Text := DSViewPedidos.DataSet.FieldByName('ValorTotalPedidos').AsString;
    end;
    FormPedido.ShowModal;
  end;
  pAtualizacao;
end;

procedure TFViewPedidos.TratarDelete;
begin
  // Exibe uma mensagem de confirmação antes de deletar o registro
  if MessageDlg('Deseja realmente excluir este Pedido '+
     DSViewPedidos.DataSet.FieldByName('NumeroPedidos').AsString+' e seus Itens?',
     mtConfirmation, [mbYes, mbNo], 0) = mrYes then
    pCRUD(acExcluir);
end;

procedure TFViewPedidos.pAtualizacao;
begin
  FPedidoController.CarregarDadosPedidos(PedidosMemTable,
    LEFiltroNumeroPedido.Text,
    LEFiltroNomeCliente.Text,
    ENR.Text);
end;

procedure TFViewPedidos.pValorTotaldoPedido;
var
  IdPedido: Integer;
  TotalItens: Double;
begin
  // Obtém o ID do pedido
  IdPedido := DSViewPedidos.DataSet.FieldByName('NumeroPedidos').AsInteger;

  if IdPedido > 0 then
  begin
    // Calcula o total dos itens do pedido
    TotalItens := FItemPedidoController.CalcularTotalItens(IdPedido);
    ShowMessage('Valor Total do Pedido '+
                  DSViewPedidos.DataSet.FieldByName('NumeroPedidos').AsString+
                    ': ' + FormatFloat('###,##0.00',TotalItens));
  end
  else
    ShowMessage('ID do pedido inválido.');
end;

procedure TFViewPedidos.CallProdutoMaisVendido;
begin
  FViewMaisVendido := TFViewMaisVendido.Create(Application);
  FViewMaisVendido.ShowModal;
end;

procedure TFViewPedidos.BBSairClick(Sender: TObject);
begin
  DSViewPedidos.DataSet.Close;
  Close;
end;

procedure TFViewPedidos.FormClose(Sender: TObject; var Action: TCloseAction);
begin
  Action := CaFree;
end;

end.
